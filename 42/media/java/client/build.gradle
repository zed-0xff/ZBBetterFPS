plugins { id 'java' }

// Detect OS and set game paths accordingly
def osName = System.getProperty("os.name").toLowerCase()
def HOME_DIR = System.getProperty("user.home")
def GAME_DIR
def GAME_JAR

if (osName.contains("mac")) {
    // macOS
    GAME_DIR = "${HOME_DIR}/Library/Application Support/Steam/steamapps/common/ProjectZomboid/Project Zomboid.app/Contents/Java"
    GAME_JAR = "${GAME_DIR}/projectzomboid.jar"
} else if (osName.contains("win")) {
    // Windows - try common Steam installation paths
    def programFiles = System.getenv("ProgramFiles(x86)") ?: System.getenv("ProgramFiles")
    def steamPath = System.getenv("SteamPath") ?: "${programFiles}\\Steam"
    GAME_DIR = "${steamPath}\\steamapps\\common\\ProjectZomboid"
    GAME_JAR = "${GAME_DIR}\\projectzomboid.jar"
} else {
    // Linux
    GAME_DIR = "${HOME_DIR}/.steam/steam/steamapps/common/ProjectZomboid"
    GAME_JAR = "${GAME_DIR}/projectzomboid.jar"
}

dependencies {
    compileOnly files("${GAME_DIR}/ZombieBuddy.jar")
    compileOnly files(GAME_JAR)
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

// place all .java files in the 'src' directory
sourceSets {
    main {
        java {
            srcDirs = ['src']
            include '**/*.java'
        }
    }
}

// disable annotation processing, JNI headers, and incremental compilation
tasks.withType(JavaCompile).configureEach {
    options.annotationProcessorPath = files()
    options.generatedSourceOutputDirectory.set(provider { null })
    options.headerOutputDirectory.set(provider { null })
    options.incremental = false
}

tasks.register("cleanup") {
    doLast {
        delete "${buildDir}/tmp"
        delete "${buildDir}/classes"
    }
}

tasks.named("build") {
    finalizedBy("cleanup")
}

